default:
    @just --list

clean:
    -test -e .out && rm -r .out
    mkdir .out

purge: clean

build: clean build-grub build-linux build-rust

GRUB_VERSION := "2.06"

build-grub:
    curl https://ftp.gnu.org/gnu/grub/grub-{{GRUB_VERSION}}.tar.xz -o .out/grub.tar.xz
    cd .out && tar xf grub.tar.xz
    cd .out/grub-{{GRUB_VERSION}} && ./configure --target=x86_64 --with-platform=efi --disable-werror && make

    mv .out/grub-{{GRUB_VERSION}} .out/grub

LINUX_VERSION := "linux-6.7.2"

build-linux:
    curl https://cdn.kernel.org/pub/linux/kernel/v6.x/{{LINUX_VERSION}}.tar.xz -o .out/linux.tar.xz
    cd .out && tar xf linux.tar.xz
    cat custom_config | tee -a .out/{{LINUX_VERSION}}/arch/x86/configs/x86_64_defconfig
    cd .out/{{LINUX_VERSION}} && make defconfig && make all

    cp .out/{{LINUX_VERSION}}/arch/x86/boot/bzImage .out/linux.x86

RUST_DATE := "2024-02-08"
RUST_VERSION := "1.76.0"
TARGET_TRIPLE := "x86_64-unknown-linux-gnu"

build-rust:
    -test -e .out/rust-sysroot && rm -r .out/rust-sysroot
    mkdir .out/rust-sysroot

    curl https://static.rust-lang.org/dist/{{RUST_DATE}}/rustc-{{RUST_VERSION}}-{{TARGET_TRIPLE}}.tar.xz -o .out/rustc.tar.xz
    curl https://static.rust-lang.org/dist/{{RUST_DATE}}/rust-std-{{RUST_VERSION}}-{{TARGET_TRIPLE}}.tar.xz -o .out/rust-std.tar.xz

    cd .out && tar xf rustc.tar.xz
    cd .out && tar xf rust-std.tar.xz

    cp -r .out/rustc-{{RUST_VERSION}}-{{TARGET_TRIPLE}}/rustc/* .out/rust-sysroot
    cp -r .out/rust-std-{{RUST_VERSION}}-{{TARGET_TRIPLE}}/rust-std-{{TARGET_TRIPLE}}/* .out/rust-sysroot

    rm .out/rust-sysroot/manifest.in